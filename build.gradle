/*
 * MIT License
 *
 * Copyright 2020-2021 noahhusby
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    ext {
        husbylibVersion = "0.1." + ('git rev-list --count HEAD'.execute().text.trim())
        gsonVersion = "2.8.0"
        snakeVersion = "1.30"
        guavaVersion = "31.0.1-jre"
        configVersion = "1.4.2"
        mysqlVersion = "8.0.28"
        hikariVersion = "4.0.3"
        lombokVersion = "1.18.24"
        mongoVersion = "3.12.11"
    }

    group "com.noahhusby.lib"
    version "$husbylibVersion"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        if (!("common".equalsIgnoreCase(project.name))) {
            implementation project(":common")
        }

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    publishing {
        repositories {
            if (System.getenv("MAVEN_USERNAME") != null && System.getenv("MAVEN_PASSWORD")) {
                maven {
                    credentials {
                        username = System.getenv("MAVEN_USERNAME")
                        password = System.getenv("MAVEN_PASSWORD")
                    }
                    name = "HusbyLib"
                    url = "https://maven.noahhusby.com/releases"
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId = "com.noahhusby.lib"
                artifactId = "HusbyLib".equalsIgnoreCase(project.name) ? "all" : project.name
                version = project.version

                from components.java
            }
        }
    }
}

dependencies {
    for (Project prj : project.allprojects) {
        if (!"HusbyLib".equalsIgnoreCase(prj.name) && prj.parent.name.equalsIgnoreCase("HusbyLib")) {
            implementation project(":" + prj.name)
        }
    }
}